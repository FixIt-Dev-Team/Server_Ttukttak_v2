plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'

    // Static Code Analysis
    id 'org.sonarqube' version '4.4.1.3373'
    id 'checkstyle'

    // Junit5
    id 'idea'

    // Junit5 to SwaggerDocs
    id 'com.epages.restdocs-api-spec' version '0.18.2'
}

group = 'com.backend'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'backend-dev-team'
        property 'sonar.projectKey', 'FixIt-Dev-Team_Server_Ttukttak_v2'
    }
}

checkstyle {
    toolVersion = "10.14.0"
}

dependencies {

    // Web Support
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Dev Support
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Database Support
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb:3.2.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.2.5'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Docker Support
//    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // Test Support
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation(platform('org.junit:junit-bom:5.10.2'))
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.8.0'

    // Actuator Support-> 향후 모니터링 붙일 것
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Logging Support
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    // Slack
    implementation 'com.slack.api:slack-api-client:1.39.0'

    // Junit5 to SwaggerDocs
    implementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2' //2.2
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
}

openapi { //2.3
    host = 'localhost:8080'
    basePath = '/api'
    title = 'My API'
    description = 'My API description'
    //tagDescriptionsPropertiesFile = 'src/docs/tag-descriptions.yaml'
    version = '1.0.0'
    format = 'json'
}

openapi3 {
    server = 'https://localhost:8080'
    title = 'My API'
    description = 'My API description'
    //tagDescriptionsPropertiesFile = 'src/docs/tag-descriptions.yaml'
    version = '0.1.0'
    format = 'yaml'
}

postman {
    title = 'My API'
    version = '0.1.0'
    baseUrl = 'https://localhost:8080'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

tasks.register('copySwagger',Copy){
    from layout.buildDirectory.file('api-spec/openapi3.yaml')
    into layout.projectDirectory.dir('src/main/resources/static')
}
